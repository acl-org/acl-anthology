@_default:
  just -l

# ALIASES

alias ds := docs-serve
alias ft := fix-and-test
alias ta := test-all
alias py := repl

# Install the project dependencies
install *FLAGS:
  uv sync {{FLAGS}}

# Install the pre-commit hooks
install-hooks:
  uv run pre-commit install

# Run checks (hooks & type-checker)
check: && typecheck
  uv run pre-commit run --all-files

# Run checks (twice in case of failure, so autofixes are applied)
fix: && typecheck
  @uv run pre-commit run -a || uv run pre-commit run -a

# Run checks (twice in case of failure) and all tests
fix-and-test: fix test-all

# Run all tests
test-all *FLAGS:
  uv run pytest -m "not integration" {{FLAGS}}

# Run all tests and generate coverage report
test-with-coverage:
  uv run pytest -m "not integration" --cov=acl_anthology --cov-report=xml

# Run tests that operate on the acl-org/acl-anthology repo
test-integration *FLAGS='-v':
  uv run pytest {{FLAGS}} -m "integration"

# Run only test functions containing TERM
test TERM *FLAGS:
  uv run pytest -k _{{TERM}} {{FLAGS}}

# Run all tests on all supported Python versions
test-all-python-versions:
  #!/usr/bin/env bash
  set -eux
  # Loop over all supported Python versions
  for py in 3.10 3.11 3.12 3.13 3.14; do
    uv run --python $py --isolated pytest -m "not integration"
  done

# Run all tests that are expected to fail and list a summary with their reasons
test-only-xfail:
  uv run pytest -rx -m xfail

# Run type-checker only
typecheck:
  uv run mypy acl_anthology

# Update dependencies and list packages where newer versions are available
update-dependencies:
  uv sync --upgrade
  uv pip list --outdated

# Run benchmarks
benchmark:
  uv run richbench --percentage benchmarks/
  @echo ""
  @echo "  Note: Benchmark descriptions should be interpreted as 'Is it faster to...' or"
  @echo "        'What is the performance impact if we...', with the highlighted columns"
  @echo "        showing the delta."

# Build the documentation
docs:
  uv run mkdocs build

# Build and serve the documentation locally
docs-serve:
  uv run mkdocs serve

# Open a Python REPL with an Anthology object pre-instantiated
repl:
  uv run python -i repl_with_anthology.py

# Profile Anthology.load_all() and output some performance metrics
profile-load-all:
  uv run python -m acl_anthology.debug -t

# Check that there are no uncommited changes
[private]
no-uncommitted-changes:
  git update-index --refresh
  git diff-index --quiet HEAD --

# Bump version, update changelog, build new package, create a tag
prepare-new-release VERSION: no-uncommitted-changes check test-all test-integration docs
  #!/usr/bin/env bash
  set -eux
  # Set trap to revert on error
  trap 'git checkout -- CHANGELOG.md pyproject.toml' ERR
  # Bump version
  uv version {{VERSION}}
  # Update changelog
  VERSION=$(uv version --short)
  DATE=$(date -u +%Y-%m-%d)
  sed -i "s/^## \[Unreleased\].*\$/## [$VERSION] â€” $DATE/" CHANGELOG.md
  # Build package
  uv build
  # Commit changes
  git add CHANGELOG.md pyproject.toml
  git commit -m "Bump to version v$VERSION"
  # Done!
  set +x
  echo ""
  echo "#############################################################"
  echo "### New release created: $VERSION"
  echo "#############################################################"
  echo ""
  echo "(To undo: git reset HEAD~ )"
  echo ""
  echo "Next steps:"
  echo "  1. Create PR to master, then after merging:"
  echo "  2. git tag py-v$VERSION && git push --tags"
  echo "  3. uv publish"
  echo "  4. (optionally) Create a release on Github"
